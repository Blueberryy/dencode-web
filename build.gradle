buildscript {
  repositories {
    jcenter()
  }
  
  dependencies {
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
  }
}

plugins {
  id 'java'
  id 'java-library'
  id 'war'
}

apply plugin: 'com.google.cloud.tools.appengine-appyaml'


repositories {
  jcenter()
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-webapp:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-util:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-annotations:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:apache-jsp:9.4.30.v20200611'
  
  implementation 'jstl:jstl:1.2'
  implementation 'org.mifmi:mifmi-commons4j:0.2.5'
  api 'commons-codec:commons-codec:1.14'
  api 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
}

appengine {
  deploy {
    projectId = 'GCLOUD_CONFIG'
    version = 'GCLOUD_CONFIG'
    promote = false
    stopPreviousVersion = false
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'com.dencode.web.server.ServerMain'
  }
}

task copyJar(type: Copy) {
  dependsOn jar
  
  from project.libsDir
  include '*.jar'
  into appengine.stage.stagingDirectory
}

task copyDependencies(type: Copy) {
  from configurations.providedCompile
  into appengine.stage.stagingDirectory
}

appengineStage.finalizedBy copyJar, copyDependencies

task appRunStage(type: JavaExec) {
  dependsOn appengineStage
  
  environment PORT: 8080
  workingDir appengine.stage.stagingDirectory
  classpath "${appengine.stage.stagingDirectory}/*"
  main = 'com.dencode.web.server.ServerMain'
}

group = 'com.dencode'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11
